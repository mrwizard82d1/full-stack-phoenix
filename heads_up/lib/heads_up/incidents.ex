defmodule HeadsUp.Incidents do
  alias HeadsUp.Incidents.Incident
  alias HeadsUp.Repo

  import Ecto.Query

  def subscribe(incident_id) do
    Phoenix.PubSub.subscribe(HeadsUp.PubSub, "incident:#{incident_id}")
  end

  def broadcast(incident_id, message) do
    Phoenix.PubSub.broadcast(HeadsUp.PubSub, "incident:#{incident_id}", message)
  end

  def list_incidents do
    Repo.all(Incident)
  end

  def filter_incidents(filter) do
    Incident
    |> with_status(filter["status"])
    |> search_by(filter["q"])
    |> with_charity(filter["category"])
    |> sort(filter["sort_by"])
    |> preload(:category)
    |> Repo.all()
  end

  defp with_charity(query, slug) when slug in ["", nil], do: query

  # Remember it is not a problem to use the macro syntax in one function
  # (cf. `with_status/2`) and the keyword syntax in another
  # (`with_charity/2`).
  defp with_charity(query, slug) do
    # from i in query,
    #   join: c in Category,
    #   on: i.category_id == c.id,
    #   where: c.slug == ^slug

    # Can **only** be used when the association is defined in the code;
    # otherwise, use the form above.
    from i in query,
      join: c in assoc(i, :category),
      where: c.slug == ^slug
  end

  defp with_status(query, status) when status in ~w(pending canceled resolved) do
    where(query, status: ^status)
  end

  defp with_status(query, _), do: query

  defp search_by(query, q) when q in ["", nil], do: query

  defp search_by(query, q) do
    where(query, [i], ilike(i.name, ^"%#{q}%"))
  end

  defp sort(query, "name") do
    order_by(query, :name)
  end

  defp sort(query, "priority_desc") do
    order_by(query, desc: :priority)
  end

  defp sort(query, "priority_asc") do
    order_by(query, asc: :priority)
  end

  defp sort(query, "category") do
    from i in query,
      join: c in assoc(i, :category),
      order_by: c.name
  end

  defp sort(query, _) do
    order_by(query, :id)
  end

  def get_incident!(id) do
    Repo.get!(Incident, id)
    |> Repo.preload([:category, heroic_response: :user])
  end

  def urgent_incidents(incident) do
    # Simulate a slow query.
    # Process.sleep(2000)

    Incident
    |> where(status: :pending)
    |> where([i], i.id != ^incident.id)
    |> order_by(asc: :priority)
    |> limit(3)
    |> Repo.all()
  end

  def list_responses(incident) do
    incident
    |> Ecto.assoc(:responses)
    |> preload(:user)
    |> order_by(desc: :inserted_at)
    |> Repo.all()
  end
end
